import TaelinArena.Game

enum
| PPG_IDLE
| PPG_COLORFUL_MAGIC
| PPG_DASH
| PPG_LASER_BURST
| PPG_COSMIC_ICE_BALL_CASTING
| PPG_BLOCK
| PPG_TAUNT

ppg_fun(self: Thing) : Thing
  let self = self <= thing(mov = 4, mhp = 24)

  case self |thing switch self.act

  |PPG_IDLE
    if is_walking(self)
    then animate(self, 1, PPG_WALK_000, 3, 6)
    else animate(self, 1, PPG_IDLE_000, 10, 20)

  // Left 
  |PPG_COLORFUL_MAGIC
    let self = animate(self, 0, PPG_COLORFUL_MAGIC_000, 30, 60)
    let h0   = [hit([root(2*ONE_SEC)], at_dist(self, 70), self.dir, cbox(15))]
    let h1   = [hit([damage(2)],       at_dist(self, 70), self.dir, cbox(15))]
    let h2   = [hit([damage(3)],       at_dist(self, 70), self.dir, cbox(15))]
    let self = cast(self, 20, h0)
    let self = cast(self, 25, h1)
    let self = cast(self, 50, h2)
    self 

  // Middle
  |PPG_DASH
    let self = animate_with_blink(self, 1, PPG_DASH_000, PPG_IDLE_000, 9, 0, 9) 
    let self = blink(self, 9, at_dist(self, 100))
    self 

  // Right
  |PPG_LASER_BURST
    let effs = [damage(2)]
    let hits = [hit(effs, at_dist(self, 75), self.dir, cbox(15))]
    // separate cast from effect?
    let self = animate(self, 0, PPG_LASER_BURST_000, 26, 35)
    let self = cast(self, 28, hits)  
    self 

  // Ultimate Q
  |PPG_COSMIC_ICE_BALL_CASTING
    let self = animate(self, 0, PPG_COSMIC_ICE_BALL_CAST_000, 23, 50)
    let ice = new_thing <= thing(fun=ppg_ice_ball_effect, box=nbox)
    let self = spawn(self, 23, [move(ice, at_max_dist(self, 100))])
    self

  // Space
  | PPG_BLOCK
    let self = set_thing_buf(self, cons(_ shielded(29, 1000), self.buf))
    let self = animate_between(self, 0, PPG_BLOCK_000, 8, 0, 14)
    let self = animate_between(self, 0, PPG_BLOCK_008, 1, 14, 20)
    let self = animate_between(self, 0, PPG_BLOCK_009, 9, 20, 29)
    let self = reset(self, 29)
    self 

  // E
  | PPG_TAUNT
    animate(self, 0, PPG_TAUNT_000, 12, 24)

  else self

ppg_ice_ball_effect(self: Thing) : Thing
  case self |thing 
  let self = animate_die(self, 0, PPG_COSMIC_ICE_BALL_000, 30, 60)
  // TODO: add haste effect for allies
  let effs = [damage(2.5), root(2.5*ONE_SEC)]
  let big_circle_pos = add_v3(self.pos, v3(4,2,0))
  let small_circle_1_pos = add_v3(v3( -3,-45, 0), big_circle_pos) 
  let small_circle_2_pos = add_v3(v3(-41, 23, 0), big_circle_pos) 
  let small_circle_3_pos = add_v3(v3( 41, 23, 0), big_circle_pos)
  let hits = [
    //big circle
    hit(effs, big_circle_pos, self.dir, cbox(30)),
    //smaller circle 1
    hit(effs, small_circle_1_pos, self.dir, cbox(16)),
    //smaller circle 2
    hit(effs, small_circle_2_pos, self.dir, cbox(16)),
    //smaller circle 3
    hit(effs, small_circle_3_pos, self.dir, cbox(16)),
  ]
  let self = cast(self, 45, hits)
  let self = cast(self, 55, hits)
  self 
  

  

